module Rollouts

export rollout
export open_rollout
export unitary_rollout
export lab_frame_unitary_rollout
export lab_frame_unitary_rollout_trajectory

export rollout_fidelity
export unitary_rollout_fidelity
export open_rollout_fidelity

using ..DirectSums

using NamedTrajectories
using QuantumCollocationCore
using PiccoloQuantumObjects
using ExponentialAction
using LinearAlgebra
using ProgressMeter
using TestItems
using ForwardDiff

# ----------------------------------------------------------------------------- #

"""
    infer_is_evp(integrator::Function)

Infer whether the integrator is a exponential-vector product (EVP) function.

If `true`, the integrator is expected to have a signature like the exponential action,
`expv`. Otherwise, it is expected to have a signature like `exp`.
"""
function infer_is_evp(integrator::Function)
    # name + args
    ns = fieldcount.([m.sig for m âˆˆ methods(integrator)])
    is_exp = 2 âˆˆ ns
    is_expv = 4 âˆˆ ns
    if is_exp && is_expv
        throw(ErrorException("Ambiguous rollout integrator signature. Please specify manually."))
    elseif is_exp
        return false
    elseif is_expv
        return true
    else
        throw(ErrorException("No valid rollout integrator signature found."))
    end
end

# ----------------------------------------------------------------------------- #
# Quantum state rollouts
# ----------------------------------------------------------------------------- #

@doc raw"""
    rollout(
        ÏˆÌƒ_init::AbstractVector{<:Float64},
        controls::AbstractMatrix,
        Î”t::AbstractVector,
        system::AbstractQuantumSystem
    )

Rollout a quantum state `ÏˆÌƒ_init` under the control `controls` for a time `Î”t`
using the system `system`.

If `exp_vector_product` is `true`, the integrator is expected to have a signature like
the exponential action, `expv`. Otherwise, it is expected to have a signature like `exp`.

Types should allow for autodifferentiable controls and times.
"""
function rollout(
    ÏˆÌƒ_init::AbstractVector{<:Real},
    controls::AbstractMatrix,
    Î”t::AbstractVector,
    system::AbstractQuantumSystem;
    show_progress=false,
    integrator=expv,
    exp_vector_product=infer_is_evp(integrator),
)
    T = size(controls, 2)

    # Enable ForwardDiff
    R = Base.promote_eltype(ÏˆÌƒ_init, controls, Î”t)
    Î¨Ìƒ = zeros(R, length(ÏˆÌƒ_init), T)

    Î¨Ìƒ[:, 1] .= ÏˆÌƒ_init

    p = Progress(T-1; enabled=show_progress)
    for t = 2:T
        aâ‚œâ‚‹â‚ = controls[:, t - 1]
        Gâ‚œ = system.G(aâ‚œâ‚‹â‚)
        if exp_vector_product
            Î¨Ìƒ[:, t] .= integrator(Î”t[t - 1], Gâ‚œ, Î¨Ìƒ[:, t - 1])
        else
            Î¨Ìƒ[:, t] .= integrator(Matrix(Gâ‚œ) * Î”t[t - 1]) * Î¨Ìƒ[:, t - 1]
        end
        next!(p)
    end

    return Î¨Ìƒ
end

rollout(Ïˆ::Vector{<:Complex}, args...; kwargs...) =
    rollout(ket_to_iso(Ïˆ), args...; kwargs...)

function rollout(
    inits::AbstractVector{<:AbstractVector}, args...; kwargs...
)
    return vcat([rollout(state, args...; kwargs...) for state âˆˆ inits]...)
end

function rollout_fidelity(
    ÏˆÌƒ_init::AbstractVector{<:Real},
    ÏˆÌƒ_goal::AbstractVector{<:Real},
    controls::AbstractMatrix,
    Î”t::AbstractVector,
    system::AbstractQuantumSystem;
    kwargs...
)
    Î¨Ìƒ = rollout(ÏˆÌƒ_init, controls, Î”t, system; kwargs...)
    return iso_fidelity(Î¨Ìƒ[:, end], ÏˆÌƒ_goal)
end

function rollout_fidelity(
    Ïˆ_init::AbstractVector{<:Complex},
    Ïˆ_goal::AbstractVector{<:Complex},
    controls::AbstractMatrix,
    Î”t::AbstractVector,
    system::AbstractQuantumSystem;
    kwargs...
)
    return rollout_fidelity(ket_to_iso(Ïˆ_init), ket_to_iso(Ïˆ_goal), controls, Î”t, system; kwargs...)
end

function rollout_fidelity(
    trajectory::NamedTrajectory,
    system::AbstractQuantumSystem;
    state_name::Symbol=:ÏˆÌƒ,
    control_name=:a,
    kwargs...
)
    fids = []
    for name âˆˆ trajectory.names
        if startswith(name, state_name)
            controls = trajectory[control_name]
            init = trajectory.initial[name]
            goal = trajectory.goal[name]
            fid = rollout_fidelity(init, goal, controls, get_timesteps(trajectory), system; kwargs...)
            push!(fids, fid)
        end
    end
    return length(fids) == 1 ? fids[1] : fids
end

function rollout_fidelity(
    prob::QuantumControlProblem,
    system::AbstractQuantumSystem;
    kwargs...
)
    return rollout_fidelity(prob.trajectory, system; kwargs...)
end

# ----------------------------------------------------------------------------- #
# Open quantum system rollouts
# ----------------------------------------------------------------------------- #

"""
    open_rollout(
        Ïâƒ—â‚::AbstractVector{<:Complex},
        controls::AbstractMatrix,
        Î”t::AbstractVector,
        system::AbstractQuantumSystem;
        kwargs...
    )

Rollout a quantum state `Ïâƒ—â‚` under the control `controls` for a time `Î”t`

# Arguments
- `Ïâƒ—â‚::AbstractVector{<:Complex}`: Initial state vector
- `controls::AbstractMatrix`: Control matrix
- `Î”t::AbstractVector`: Time steps
- `system::AbstractQuantumSystem`: Quantum system

# Keyword Arguments
- `show_progress::Bool=false`: Show progress bar
- `integrator::Function=expv`: Integrator function
- `exp_vector_product::Bool`: Infer whether the integrator is an exponential-vector product

"""
function open_rollout(
    Ïâƒ—Ìƒ_init::AbstractVector{<:Real},
    controls::AbstractMatrix,
    Î”t::AbstractVector,
    system::AbstractQuantumSystem;
    show_progress=false,
    integrator=expv,
    exp_vector_product=infer_is_evp(integrator),
)
    T = size(controls, 2)

    # Enable ForwardDiff
    R = Base.promote_eltype(Ïâƒ—Ìƒ_init, controls, Î”t)
    Ïâƒ—Ìƒ = zeros(R, length(Ïâƒ—Ìƒ_init), T)

    Ïâƒ—Ìƒ[:, 1] = Ïâƒ—Ìƒ_init

    p = Progress(T-1; enabled=show_progress)
    for t = 2:T
        aâ‚œâ‚‹â‚ = controls[:, t - 1]
        ð’¢â‚œ = system.ð’¢(aâ‚œâ‚‹â‚)
        if exp_vector_product
            Ïâƒ—Ìƒ[:, t] = integrator(Î”t[t - 1], ð’¢â‚œ, Ïâƒ—Ìƒ[:, t - 1])
        else
            Ïâƒ—Ìƒ[:, t] = integrator(Î”t[t - 1], ð’¢â‚œ) * Ïâƒ—Ìƒ[:, t - 1]
        end
        next!(p)
    end

    return Ïâƒ—Ìƒ
end

"""
    open_rollout(
        Ïâ‚::AbstractMatrix{<:Complex},
        controls::AbstractMatrix,
        Î”t::AbstractVector,
        system::AbstractQuantumSystem;
        kwargs...
    )

Rollout a density matrix `Ïâ‚` under the control `controls` and timesteps `Î”t`

"""
function open_rollout(
    Ï_init::AbstractMatrix{<:Complex},
    controls::AbstractMatrix,
    Î”t::AbstractVector,
    system::AbstractQuantumSystem;
    kwargs...
)
    return open_rollout(density_to_iso_vec(Ï_init), controls, Î”t, system; kwargs...)
end

function open_rollout_fidelity(
    Ï_init::AbstractMatrix{<:Complex},
    Ï_goal::AbstractMatrix{<:Complex},
    controls::AbstractMatrix,
    Î”t::AbstractVector,
    system::AbstractQuantumSystem;
    kwargs...
)

    Ïâƒ—Ìƒ_traj = open_rollout(Ï_init, controls, Î”t, system; kwargs...)
    Ï_final = iso_vec_to_density(Ïâƒ—Ìƒ_traj[:, end])
    return real(tr(Ï_goal * Ï_final))
end

function open_rollout_fidelity(
    traj::NamedTrajectory,
    system::OpenQuantumSystem;
    state_name::Symbol=:Ïâƒ—Ìƒ,
    control_name::Symbol=:a,
    kwargs...
)
    Ï_goal = iso_vec_to_density(traj.goal[state_name])
    Ï_init = iso_vec_to_density(traj.initial[state_name])
    controls = traj[control_name]
    Î”t = get_timesteps(traj)
    return open_rollout_fidelity(Ï_init, Ï_goal, controls, Î”t, system; kwargs...)
end


# ----------------------------------------------------------------------------- #
# Unitary rollouts
# ----------------------------------------------------------------------------- #

function unitary_rollout(
    UÌƒâƒ—_init::AbstractVector{R1},
    controls::AbstractMatrix{R2},
    Î”t::AbstractVector{R3},
    system::AbstractQuantumSystem;
    show_progress=false,
    integrator=expv,
    exp_vector_product=infer_is_evp(integrator),
) where {R1 <: Real, R2 <: Real, R3 <: Real}
    T = size(controls, 2)

    # Enable ForwardDiff
    R = Base.promote_eltype(UÌƒâƒ—_init, controls, Î”t)
    UÌƒâƒ— = zeros(R, length(UÌƒâƒ—_init), T)

    UÌƒâƒ—[:, 1] .= UÌƒâƒ—_init

    p = Progress(T-1; enabled=show_progress)
    for t = 2:T
        aâ‚œâ‚‹â‚ = controls[:, t - 1]
        Gâ‚œ = system.G(aâ‚œâ‚‹â‚)
        UÌƒâ‚œâ‚‹â‚ = iso_vec_to_iso_operator(UÌƒâƒ—[:, t - 1])
        if exp_vector_product
            UÌƒâ‚œ = integrator(Î”t[t - 1], Gâ‚œ, UÌƒâ‚œâ‚‹â‚)
        else
            UÌƒâ‚œ = integrator(Matrix(Gâ‚œ) * Î”t[t - 1]) * UÌƒâ‚œâ‚‹â‚
        end
        UÌƒâƒ—[:, t] .= iso_operator_to_iso_vec(UÌƒâ‚œ)
        next!(p)
    end

    return UÌƒâƒ—
end

function unitary_rollout(
    controls::AbstractMatrix,
    Î”t::AbstractVector,
    system::AbstractQuantumSystem;
    kwargs...
)
    IÌƒâƒ— = operator_to_iso_vec(Matrix{ComplexF64}(I(system.levels)))
    return unitary_rollout(IÌƒâƒ—, controls, Î”t, system; kwargs...)
end

function unitary_rollout(
    traj::NamedTrajectory,
    system::AbstractQuantumSystem;
    unitary_name::Symbol=:UÌƒâƒ—,
    drive_name::Symbol=:a,
    kwargs...
)
    return unitary_rollout(
        traj.initial[unitary_name],
        traj[drive_name],
        get_timesteps(traj),
        system;
        kwargs...
    )
end

function unitary_rollout_fidelity(
    UÌƒâƒ—_init::AbstractVector{<:Real},
    UÌƒâƒ—_goal::AbstractVector{<:Real},
    controls::AbstractMatrix,
    Î”t::AbstractVector,
    system::AbstractQuantumSystem;
    subspace::AbstractVector{Int}=axes(iso_vec_to_operator(UÌƒâƒ—_goal), 1),
    phases::Union{Nothing, AbstractVector{<:Real}}=nothing,
    phase_operators::Union{Nothing, AbstractVector{<:AbstractMatrix{<:Complex}}}=nothing,
    kwargs...
)
    UÌƒâƒ—_T = unitary_rollout(UÌƒâƒ—_init, controls, Î”t, system; kwargs...)[:, end]
    if !isnothing(phases)
        return iso_vec_unitary_free_phase_fidelity(UÌƒâƒ—_T, UÌƒâƒ—_goal, phases, phase_operators; subspace=subspace)
    else
        return iso_vec_unitary_fidelity(UÌƒâƒ—_T, UÌƒâƒ—_goal; subspace=subspace)
    end
end

function unitary_rollout_fidelity(
    UÌƒâƒ—_goal::AbstractVector{<:Real},
    controls::AbstractMatrix,
    Î”t::AbstractVector,
    system::AbstractQuantumSystem;
    kwargs...
)
    IÌƒâƒ— = operator_to_iso_vec(Matrix{ComplexF64}(I(system.levels)))
    return unitary_rollout_fidelity(IÌƒâƒ—, UÌƒâƒ—_goal, controls, Î”t, system; kwargs...)
end

function unitary_rollout_fidelity(
    U_init::AbstractMatrix{<:Complex},
    U_goal::AbstractMatrix{<:Complex},
    controls::AbstractMatrix,
    Î”t::AbstractVector,
    system::AbstractQuantumSystem;
    kwargs...
)
    UÌƒâƒ—_init = operator_to_iso_vec(U_init)
    UÌƒâƒ—_goal = operator_to_iso_vec(U_goal)
    return unitary_rollout_fidelity(UÌƒâƒ—_init, UÌƒâƒ—_goal, controls, Î”t, system; kwargs...)
end

unitary_rollout_fidelity(
    U_goal::AbstractMatrix{<:Complex},
    controls::AbstractMatrix,
    Î”t::AbstractVector,
    system::AbstractQuantumSystem;
    kwargs...
) = unitary_rollout_fidelity(operator_to_iso_vec(U_goal), controls, Î”t, system; kwargs...)

unitary_rollout_fidelity(
    U_goal::EmbeddedOperator,
    controls::AbstractMatrix,
    Î”t::AbstractVector,
    system::AbstractQuantumSystem;
    subspace::AbstractVector{Int}=U_goal.subspace,
    kwargs...
) = unitary_rollout_fidelity(U_goal.operator, controls, Î”t, system; subspace=subspace, kwargs...)

function unitary_rollout_fidelity(
    traj::NamedTrajectory,
    sys::AbstractQuantumSystem;
    unitary_name::Symbol=:UÌƒâƒ—,
    drive_name::Symbol=:a,
    kwargs...
)
    UÌƒâƒ—_init = traj.initial[unitary_name]
    UÌƒâƒ—_goal = traj.goal[unitary_name]
    controls = traj[drive_name]
    Î”t = get_timesteps(traj)
    return unitary_rollout_fidelity(UÌƒâƒ—_init, UÌƒâƒ—_goal, controls, Î”t, sys; kwargs...)
end

# ----------------------------------------------------------------------------- #
# Experimental rollouts
# ----------------------------------------------------------------------------- #

unitary_rollout_fidelity(
    U_goal::EmbeddedOperator,
    controls::AbstractMatrix{Float64},
    Î”t::Union{AbstractVector{Float64}, AbstractMatrix{Float64}, Float64},
    sys::AbstractQuantumSystem;
    subspace=U_goal.subspace,
    kwargs...
) = unitary_rollout_fidelity(U_goal.operator, controls, Î”t, sys; subspace=subspace, kwargs...)

"""
    lab_frame_unitary_rollout(
        sys::AbstractQuantumSystem,
        controls::AbstractMatrix{Float64};
        duration=nothing,
        timestep=nothing,
        Ï‰=nothing,
        timestep_nyquist=1 / (50 * Ï‰)
    )
"""
function lab_frame_unitary_rollout(
    sys::AbstractQuantumSystem,
    controls::AbstractMatrix{Float64};
    duration=nothing,
    timestep=nothing,
    Ï‰=nothing,
    timestep_nyquist=1 / (50 * Ï‰)
)
    @assert !isnothing(duration) "must specify duration"
    @assert !isnothing(timestep) "must specify timestep"
    @assert !isnothing(Ï‰) "must specify Ï‰"

    controls_upsampled, times_upsampled =
        upsample(controls, duration, timestep, timestep_nyquist)

    u = controls_upsampled[1, :]
    v = controls_upsampled[2, :]

    c = cos.(2Ï€ * Ï‰ * times_upsampled)
    s = sin.(2Ï€ * Ï‰ * times_upsampled)

    pulse = stack([u .* c - v .* s, u .* s + v .* c], dims=1)

    return unitary_rollout(pulse, timestep_nyquist, sys), pulse
end

function lab_frame_unitary_rollout_trajectory(
    sys_lab_frame::AbstractQuantumSystem,
    traj_rotating_frame::NamedTrajectory,
    op_lab_frame::EmbeddedOperator;
    timestep_nyquist=1/(400 * sys_lab_frame.params[:Ï‰]),
    control_name::Symbol=:a,
)::NamedTrajectory
    @assert sys_lab_frame.params[:lab_frame] "QuantumSystem must be in the lab frame"

    UÌƒâƒ—_labframe, pulse = lab_frame_unitary_rollout(
        sys_lab_frame,
        traj_rotating_frame[control_name];
        duration=get_times(traj_rotating_frame)[end],
        timestep=traj_rotating_frame.Î”t[end],
        Ï‰=sys_lab_frame.params[:Ï‰],
        timestep_nyquist=timestep_nyquist
    )

    return NamedTrajectory(
        (
            UÌƒâƒ—=UÌƒâƒ—_labframe,
            a=pulse
        );
        timestep=timestep_nyquist,
        controls=(control_name,),
        initial=(
            UÌƒâƒ—=UÌƒâƒ—_labframe[:, 1],
        ),
        goal=(
            UÌƒâƒ—=operator_to_iso_vec(op_lab_frame.operator),
        )
    )
end

# *************************************************************************** #

@testitem "Test rollouts using fidelities" begin
    using ExponentialAction

    sys = QuantumSystem(0 * GATES[:Z], [GATES[:X], GATES[:Y]])
    U_goal = GATES[:X]
    embedded_U_goal = EmbeddedOperator(U_goal, sys)
    T = 51
    Î”t = 0.2
    ts = fill(Î”t, T)
    as = collect([Ï€/(T-1)/Î”t * sin.(Ï€*(0:T-1)/(T-1)).^2 zeros(T)]')

    prob = UnitarySmoothPulseProblem(
        sys, U_goal, T, Î”t, a_guess=as,
        ipopt_options=IpoptOptions(print_level=1),
        piccolo_options=PiccoloOptions(verbose=false, free_time=false)
    )

    Ïˆ = ComplexF64[1, 0]
    Ïˆ_goal = U_goal * Ïˆ
    ÏˆÌƒ = ket_to_iso(Ïˆ)
    ÏˆÌƒ_goal = ket_to_iso(Ïˆ_goal)

    # Default integrator
    # State fidelity
    @test rollout_fidelity(Ïˆ, Ïˆ_goal, as, ts, sys) â‰ˆ 1

    # Unitary fidelity
    @test unitary_rollout_fidelity(U_goal, as, ts, sys) â‰ˆ 1
    @test unitary_rollout_fidelity(prob.trajectory, sys) â‰ˆ 1
    @test unitary_rollout_fidelity(embedded_U_goal, as, ts, sys) â‰ˆ 1

    # Free phase unitary
    @test unitary_rollout_fidelity(prob.trajectory, sys;
        phases=[0.0], phase_operators=Matrix{ComplexF64}[PAULIS[:Z]]
    ) â‰ˆ 1

    # Free phase unitary
    @test unitary_rollout_fidelity(prob.trajectory, sys;
        phases=[0.0],
        phase_operators=[PAULIS[:Z]]
    ) â‰ˆ 1

    # Expv explicit
    # State fidelity
    @test rollout_fidelity(Ïˆ, Ïˆ_goal, as, ts, sys, integrator=expv) â‰ˆ 1

    # Unitary fidelity
    @test unitary_rollout_fidelity(U_goal, as, ts, sys, integrator=expv) â‰ˆ 1
    @test unitary_rollout_fidelity(prob.trajectory, sys, integrator=expv) â‰ˆ 1
    @test unitary_rollout_fidelity(embedded_U_goal, as, ts, sys, integrator=expv) â‰ˆ 1

    # Exp explicit
    # State fidelity
    @test rollout_fidelity(Ïˆ, Ïˆ_goal, as, ts, sys, integrator=exp) â‰ˆ 1

    # Unitary fidelity
    @test unitary_rollout_fidelity(U_goal, as, ts, sys, integrator=exp) â‰ˆ 1
    @test unitary_rollout_fidelity(prob.trajectory, sys, integrator=exp) â‰ˆ 1
    @test unitary_rollout_fidelity(embedded_U_goal, as, ts, sys, integrator=exp) â‰ˆ 1

    # Bad integrator
    @test_throws ErrorException unitary_rollout_fidelity(U_goal, as, ts, sys, integrator=(a,b) -> 1) â‰ˆ 1
end

@testitem "Foward diff rollout" begin
    using ForwardDiff
    using ExponentialAction

    sys = QuantumSystem(0 * GATES[:Z], [GATES[:X], GATES[:Y]])
    T = 51
    Î”t = 0.2
    ts = fill(Î”t, T)
    as = collect([Ï€/(T-1)/Î”t * sin.(Ï€*(0:T-1)/(T-1)).^2 zeros(T)]')

    # Control derivatives
    Ïˆ = ComplexF64[1, 0]
    result1 = ForwardDiff.jacobian(
        as -> rollout(Ïˆ, as, ts, sys, integrator=expv)[:, end], as
    )
    iso_ket_dim = length(ket_to_iso(Ïˆ))
    @test size(result1) == (iso_ket_dim, T * sys.n_drives)

    result2 = ForwardDiff.jacobian(
        as -> unitary_rollout(as, ts, sys, integrator=expv)[:, end], as
    )
    iso_vec_dim = length(operator_to_iso_vec(sys.H(zeros(sys.n_drives))))
    @test size(result2) == (iso_vec_dim, T * sys.n_drives)

    # Time derivatives
    Ïˆ = ComplexF64[1, 0]
    result1 = ForwardDiff.jacobian(
        ts -> rollout(Ïˆ, as, ts, sys, integrator=expv)[:, end], ts
    )
    iso_ket_dim = length(ket_to_iso(Ïˆ))
    @test size(result1) == (iso_ket_dim, T)

    result2 = ForwardDiff.jacobian(
        ts -> unitary_rollout(as, ts, sys, integrator=expv)[:, end], ts
    )
    iso_vec_dim = length(operator_to_iso_vec(sys.H(zeros(sys.n_drives))))
    @test size(result2) == (iso_vec_dim, T)
end
end
